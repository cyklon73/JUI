plugins {
    id 'java'
    id 'maven-publish'
}

group = 'de.cyklon'
version = '1.0.1'


var commit = getCommit()
var release = System.getenv("RELEASE") == "true"

repositories {
    mavenCentral()
    maven { url 'https://maven.mineking.dev/releases' }
    maven { url 'https://maven.mineking.dev/snapshots' }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:22.0.0'

    implementation 'de.cyklon:JEvent:1.1.0'
    implementation 'de.cyklon:JResource:1.1.1'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            url = "https://maven.mineking.dev/" + (release ? "releases" : "snapshots")
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_SECRET")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = "de.cyklon"
            artifactId = "JUI"
            version = release ? this.version : commit

            from components.java
        }
    }
}

test {
    useJUnitPlatform()
}

static String getCommit() {
    try {
        var commit = 'git rev-parse --verify --short HEAD'.execute().text.trim()
        return commit.isEmpty() ? 'unknown' : commit
    } catch(IOException e) {
        throw new RuntimeException(e)
    }
}
